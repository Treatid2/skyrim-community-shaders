#include "Common/Color.hlsli"
#include "Common/FrameBuffer.hlsli"
#include "Common/VR.hlsli"

struct VS_INPUT
{
	float4 Position: POSITION0;

#if defined(TEX) || defined(HORIZFADE)
	float2 TexCoord: TEXCOORD0;
#endif

	float4 Color: COLOR0;
#if defined(VR)
	uint InstanceID: SV_INSTANCEID;
#endif  // VR
};

struct VS_OUTPUT
{
	float4 Position: SV_POSITION0;

#if defined(DITHER) && defined(TEX)
	float4 TexCoord0: TEXCOORD0;
#elif defined(DITHER)
	float2 TexCoord0: TEXCOORD3;
#elif defined(TEX) || defined(HORIZFADE)
	float2 TexCoord0: TEXCOORD0;
#endif

#if defined(TEXLERP)
	float2 TexCoord1: TEXCOORD1;
#endif

#if defined(HORIZFADE)
	float TexCoord2: TEXCOORD2;
#endif

#if defined(TEX) || defined(DITHER) || defined(HORIZFADE)
	float4 Color: COLOR0;
#endif

	float4 WorldPosition: POSITION1;
	float4 PreviousWorldPosition: POSITION2;
#if defined(VR)
	float ClipDistance: SV_ClipDistance0;  // o11
	float CullDistance: SV_CullDistance0;  // p11
	uint EyeIndex: EYEIDX0;
#endif  // VR
};

#ifdef VSHADER
cbuffer PerGeometry : register(b2)
{
#	if !defined(VR)
	row_major float4x4 WorldViewProj[1] : packoffset(c0);
	row_major float4x4 World[1] : packoffset(c4);
	row_major float4x4 PreviousWorld[1] : packoffset(c8);
	float3 EyePosition[1] : packoffset(c12);
	float VParams : packoffset(c12.w);
	float4 BlendColor[3] : packoffset(c13);
	float2 TexCoordOff : packoffset(c16);
#	else
	row_major float4x4 WorldViewProj[2] : packoffset(c0);
	row_major float4x4 World[2] : packoffset(c8);
	row_major float4x4 PreviousWorld[2] : packoffset(c16);
	float3 EyePosition[2] : packoffset(c24);
	float VParams : packoffset(c25.w);
	float4 BlendColor[3] : packoffset(c26);
	float2 TexCoordOff : packoffset(c29);
#	endif  // !VR
};

VS_OUTPUT main(VS_INPUT input)
{
	VS_OUTPUT vsout;
	uint eyeIndex = Stereo::GetEyeIndexVS(
#	if defined(VR)
		input.InstanceID
#	endif
	);

	float4 inputPosition = float4(input.Position.xyz, 1.0);

#	if defined(OCCLUSION)

	// Intentionally left blank

#	elif defined(MOONMASK)

	vsout.TexCoord0 = input.TexCoord;
	vsout.Color = float4(VParams.xxx, 1.0);

#	elif defined(HORIZFADE)

	float worldHeight = mul(World[eyeIndex], inputPosition).z;
	float eyeHeightDelta = -EyePosition[eyeIndex].z + worldHeight;

	vsout.TexCoord0.xy = input.TexCoord;
	vsout.TexCoord2.x = saturate((1.0 / 17.0) * eyeHeightDelta);
	vsout.Color.xyz = BlendColor[0].xyz * VParams;
	vsout.Color.w = BlendColor[0].w;

#	else  // MOONMASK HORIZFADE

#		if defined(DITHER)

#			if defined(TEX)
	vsout.TexCoord0.xyzw = input.TexCoord.xyxy * float4(1.0, 1.0, 501.0, 501.0);
#			else
	float3 inputDirection = normalize(input.Position.xyz);
	inputDirection.y += inputDirection.z;

	vsout.TexCoord0.x = 501 * acos(inputDirection.x);
	vsout.TexCoord0.y = 501 * asin(inputDirection.y);
#			endif  // TEX

#		elif defined(CLOUDS)
	vsout.TexCoord0.xy = TexCoordOff + input.TexCoord;
#		else
	vsout.TexCoord0.xy = input.TexCoord;
#		endif  // DITHER CLOUDS

#		ifdef TEXLERP
	vsout.TexCoord1.xy = TexCoordOff + input.TexCoord;
#		endif  // TEXLERP

	float3 skyColor = BlendColor[0].xyz * input.Color.xxx + BlendColor[1].xyz * input.Color.yyy +
	                  BlendColor[2].xyz * input.Color.zzz;

	vsout.Color.xyz = VParams * skyColor;
	vsout.Color.w = BlendColor[0].w * input.Color.w;

#	endif  // OCCLUSION MOONMASK HORIZFADE

	vsout.Position = mul(WorldViewProj[eyeIndex], inputPosition).xyww;
	vsout.WorldPosition = mul(World[eyeIndex], inputPosition);
	vsout.PreviousWorldPosition = mul(PreviousWorld[eyeIndex], inputPosition);

#	ifdef VR
	vsout.EyeIndex = eyeIndex;
	Stereo::VR_OUTPUT VRout = Stereo::GetVRVSOutput(vsout.Position, eyeIndex);
	vsout.Position = VRout.VRPosition;
	vsout.ClipDistance.x = VRout.ClipDistance;
	vsout.CullDistance.x = VRout.CullDistance;
#	endif  // VR
	return vsout;
}
#endif

typedef VS_OUTPUT PS_INPUT;

struct PS_OUTPUT
{
	float4 Color: SV_Target0;
	float4 MotionVectors: SV_Target1;
	float4 Normal: SV_Target2;
#if defined(CLOUD_SHADOWS) && defined(CLOUDS) && !defined(DEFERRED)
	float4 CloudShadows: SV_Target3;
#endif
};

#ifdef PSHADER
SamplerState SampBaseSampler : register(s0);
SamplerState SampBlendSampler : register(s1);
SamplerState SampNoiseGradSampler : register(s2);

Texture2D<float4> TexBaseSampler : register(t0);
Texture2D<float4> TexBlendSampler : register(t1);
Texture2D<float4> TexNoiseGradSampler : register(t2);

cbuffer PerGeometry : register(b2)
{
	float2 PParams : packoffset(c0);
};

#	if !defined(VR)
cbuffer AlphaTestRefCB : register(b11)
{
	float AlphaTestRefRS : packoffset(c0);
}
#	endif

#	include "Common/MotionBlur.hlsli"
#	include "Common/SharedData.hlsli"

#	if defined(CLOUD_SHADOWS)
#		include "CloudShadows/CloudShadows.hlsli"
#	endif

Texture2D<float> TexDepthSampler : register(t17);

/// Compose authored inputs before LL adjustment; legacy textures are already adjusted.
float3 ComposeSkyColor(float3 skyColor, float3 textureColor, float3 skyOffset, bool composeAuthoredSky)
{
	if (composeAuthoredSky)
		return Color::Sky(skyColor * textureColor + skyOffset);
	return Color::Sky(skyColor) * textureColor + Color::Sky(skyOffset);
}

PS_OUTPUT main(PS_INPUT input)
{
	PS_OUTPUT psout;
	float3 skyOffset = PParams.yyy;
	const bool composeAuthoredSky = Color::UseLinearLightingColorAdjustments();
#	if !defined(VR)
	uint eyeIndex = 0;
#	else
	uint eyeIndex = input.EyeIndex;
#	endif  // !VR

#	ifndef OCCLUSION
#		ifndef TEXLERP
	float4 baseColor = TexBaseSampler.Sample(SampBaseSampler, input.TexCoord0.xy);
	if (!composeAuthoredSky)
		baseColor.xyz = Color::Sky(baseColor.xyz);
#			ifdef TEXFADE
	baseColor.w *= PParams.x;
#			endif
#		else
	float4 blendColor = TexBlendSampler.Sample(SampBlendSampler, input.TexCoord1.xy);
	float4 baseColor = TexBaseSampler.Sample(SampBaseSampler, input.TexCoord0.xy);
	if (!composeAuthoredSky) {
		blendColor.xyz = Color::Sky(blendColor.xyz);
		baseColor.xyz = Color::Sky(baseColor.xyz);
	}
	baseColor = PParams.xxxx * (-baseColor + blendColor) + baseColor;
#		endif

#		if defined(CLOUDS)
	float skyBrightnessMultiplier = SharedData::adaptiveBalanceSettings.cloudBrightness;
#		else
	float skyBrightnessMultiplier = SharedData::adaptiveBalanceSettings.skyBrightness;
#		endif

#		if defined(DITHER)
#			if defined(VR)
	// Keep the VR sky stereo-stable by avoiding screen-space dither in Color::Sky().
	// If VR sky banding becomes visible, add a very small post-transform dither
	// here using sky-stable coords: input.TexCoord0.zw for TEX, input.TexCoord0.xy otherwise.
#				ifdef TEX
	psout.Color.xyz = ComposeSkyColor(input.Color.xyz, baseColor.xyz, skyOffset, composeAuthoredSky) * skyBrightnessMultiplier;
	psout.Color.w = baseColor.w * input.Color.w;
#				else
	psout.Color.xyz = ComposeSkyColor(input.Color.xyz, 1.0, skyOffset, composeAuthoredSky) * skyBrightnessMultiplier;
	psout.Color.w = input.Color.w;
#				endif  // TEX
#			else
	float2 noiseGradUv = float2(0.125, 0.125) * input.Position.xy;
	// The 8x8 engine dither texture stores 0..63, so center it without shifting the weather color.
	const float noiseGradCenter = 31.5 / 255.0;
	float noiseGrad =
		(TexNoiseGradSampler.Sample(SampNoiseGradSampler, noiseGradUv).x - noiseGradCenter) * 0.03125;

#				ifdef TEX
	float3 skyVertColor = composeAuthoredSky ? (input.Color.xyz + noiseGrad) : input.Color.xyz;
	float3 sunGlareColor = ComposeSkyColor(skyVertColor, baseColor.xyz, skyOffset, composeAuthoredSky);
	// Dither/noise term is the legacy sky path contribution for gradient smoothing.
	psout.Color.xyz = (sunGlareColor * skyBrightnessMultiplier) + (composeAuthoredSky ? 0.0 : noiseGrad);
	psout.Color.w = baseColor.w * input.Color.w;
#				else
	psout.Color.xyz = ComposeSkyColor(input.Color.xyz + noiseGrad, 1.0, skyOffset, composeAuthoredSky) * skyBrightnessMultiplier;
	psout.Color.w = input.Color.w;
#				endif  // TEX
#			endif      // VR

#		elif defined(MOONMASK)
	psout.Color.xyzw = baseColor;
	if (composeAuthoredSky)
		psout.Color.xyz = Color::Sky(psout.Color.xyz);

	if (baseColor.w - AlphaTestRefRS.x < 0) {
		discard;
	}

#		elif defined(HORIZFADE)
	if (composeAuthoredSky)
		psout.Color.xyz = Color::Sky(1.5 * (input.Color.xyz * baseColor.xyz + skyOffset)) * skyBrightnessMultiplier;
	else
		psout.Color.xyz = 1.5 * (ComposeSkyColor(input.Color.xyz, baseColor.xyz, skyOffset, false) * skyBrightnessMultiplier);
	psout.Color.w = input.TexCoord2.x * (baseColor.w * input.Color.w);
#		else
	psout.Color.w = input.Color.w * baseColor.w;
	psout.Color.xyz = ComposeSkyColor(input.Color.xyz, baseColor.xyz, skyOffset, composeAuthoredSky) * skyBrightnessMultiplier;
#		endif

#	else
	psout.Color = float4(0, 0, 0, 1.0);
#	endif  // OCCLUSION

#	if defined(DITHER) && defined(TEX) && !defined(OCCLUSION)
	psout.Color.xyz *= SharedData::adaptiveBalanceSettings.sunGlareIntensity;
#	endif

#	if !defined(OCCLUSION) && !defined(MOONMASK)
#		if defined(CLOUDS)
	float saturation = SharedData::adaptiveBalanceSettings.cloudSaturation;
#		else
	float saturation = SharedData::adaptiveBalanceSettings.skySaturation;
#		endif
	if (saturation != 1.0)
		psout.Color.xyz = Color::Saturation(psout.Color.xyz, saturation);
#	endif

	float2 screenMotionVector = MotionBlur::GetSSMotionVector(input.WorldPosition, input.PreviousWorldPosition, eyeIndex);

	psout.MotionVectors = float4(screenMotionVector, 0, psout.Color.w);
	psout.Normal = float4(0.5, 0.5, 0, psout.Color.w);

#	if defined(CLOUD_SHADOWS) && defined(CLOUDS) && !defined(DEFERRED)
	psout.CloudShadows = float4(1, 1, 1, psout.Color.w);

	float depth = TexDepthSampler.Load(int3(input.Position.xy, 0));
	if (depth < input.Position.z)
		psout.Color.w = 0;

#	endif

	return psout;
}
#endif
